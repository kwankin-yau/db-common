plugins {
    id 'java-library'
    id 'scala'
    id 'maven-publish'
    id 'signing'
}

group 'com.lucendar'
version '2.1.0'

compileJava.options.encoding = 'UTF-8'
compileScala.options.encoding = 'UTF-8'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    // https://mvnrepository.com/artifact/org.scalatest/scalatest
    testImplementation 'org.scalatest:scalatest_2.13:3.2.13'


    implementation('org.scala-lang:scala-library:2.13.12')
    implementation('org.scala-lang:scala-reflect:2.13.12')

    implementation('com.typesafe.scala-logging:scala-logging_2.13:3.9.5')
    implementation 'org.checkerframework:checker-qual:3.36.0'
    implementation('org.springframework:spring-jdbc:6.0.8')
    implementation('org.springframework:spring-web:6.0.8')

    api 'com.zaxxer:HikariCP:5.0.1'
    // https://mvnrepository.com/artifact/com.github.jsqlparser/jsqlparser
    implementation group: 'com.github.jsqlparser', name: 'jsqlparser', version: '4.2'


    implementation('com.lucendar:lucendar-common:2.2.0')
    implementation('com.lucendar:server-common:3.1.0')
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'db-common'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'Spring Jdbc utility library.'
                url = 'https://github.com/kwankin-yau/db-common'

                scm {
                    connection = 'https://github.com/kwankin-yau/db-common.git'
                    developerConnection = 'https://github.com/kwankin-yau/db-common.git'
                    url = 'https://github.com/kwankin-yau/db-common'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }

    }
}

javadoc {
    options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.maven
}

